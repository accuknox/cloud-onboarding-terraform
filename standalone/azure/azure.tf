############################################
# Variables
############################################
variable "app_display_name" {
  type        = string
  default     = "AccuKnox CSPM Scanner"
  description = "Display name of the Azure AD Application"
}

variable "subscription_id" {
  type        = string
  default     = "8daf11df-39be-4fc4-af77-7b2ae0d3866e"
  description = "Azure Subscription ID. Leave empty to use current subscription"
}

############################################
# Terraform Block & Provider Versions
############################################
terraform {
  required_providers {
    azurerm = {
      source  = "hashicorp/azurerm"
      version = "~> 3.70"
    }
    azuread = {
      source  = "hashicorp/azuread"
      version = "~> 3.7"
    }
    random = {
      source  = "hashicorp/random"
    }
    local = {
      source  = "hashicorp/local"
    }
  }
}

provider "azurerm" {
  features {}
  subscription_id = var.subscription_id != "" ? var.subscription_id : null
}

provider "azuread" {}

############################################
# Azure AD Application
############################################
resource "azuread_application" "accuknox" {
  display_name = var.app_display_name

  required_resource_access {
    resource_app_id = "00000003-0000-0000-c000-000000000000"

    resource_access {
      id   = "5778995a-e1bf-45b8-affa-663a9f3f4d04"
      type = "Scope"
    }
  }
}

############################################
# Service Principal & Subscription Data
############################################
resource "azuread_service_principal" "accuknox_sp" {
  client_id = azuread_application.accuknox.client_id
}

resource "azuread_service_principal_password" "client_secret" {
  service_principal_id = azuread_service_principal.accuknox_sp.id
  end_date = timeadd(timestamp(), "8760h")
}

data "azurerm_subscription" "current" {
  subscription_id = var.subscription_id != "" ? var.subscription_id : null
}

############################################
# Built-in Reader Role Assignment
############################################
resource "azurerm_role_assignment" "reader_role" {
  scope                = data.azurerm_subscription.current.id
  role_definition_name = "Reader"
  principal_id         = azuread_service_principal.accuknox_sp.object_id
}

############################################
# Custom ML / Cognitive Services Role
############################################
resource "azurerm_role_definition" "custom_accuknox_ml_role" {
  name        = "AccuKnox-ML-Custom-Role"
  scope       = data.azurerm_subscription.current.id
  description = "Custom role for ML & Cognitive services for AccuKnox"

  permissions {
    actions = [
      "Microsoft.MachineLearningServices/workspaces/onlineEndpoints/score/action",
      "Microsoft.MachineLearningServices/workspaces/serverlessEndpoints/listKeys/action",
      "Microsoft.MachineLearningServices/workspaces/listStorageAccountKeys/action",
      "Microsoft.CognitiveServices/accounts/listKeys/action",
      "Microsoft.CognitiveServices/accounts/deployments/read"
    ]
  }

  assignable_scopes = [
    data.azurerm_subscription.current.id
  ]
}

resource "azurerm_role_assignment" "custom_ml_role_assignment" {
  scope              = data.azurerm_subscription.current.id
  role_definition_id = azurerm_role_definition.custom_accuknox_ml_role.role_definition_resource_id
  principal_id       = azuread_service_principal.accuknox_sp.object_id
}

############################################
# Additional Data Sources
############################################
data "azurerm_client_config" "current" {}

############################################
# Outputs
############################################
output "client_id" {
  value = azuread_application.accuknox.client_id
}

output "client_secret" {
  value     = azuread_service_principal_password.client_secret.value
  sensitive = true
}

output "subscription_id" {
  value = split("/", trim(data.azurerm_subscription.current.id, "/"))[1]
}


output "directory_id" {
  value = data.azurerm_client_config.current.tenant_id
}

############################################
# Save Credentials to Local File
############################################
resource "local_file" "client_secret_and_app_sub_dir_file" {
  filename = "client_secret_and_app_sub_dir.txt"
  content = <<-EOT
Client ID: ${azuread_application.accuknox.client_id}
Client Secret: ${azuread_service_principal_password.client_secret.value}
Subscription ID: ${split("/", trim(data.azurerm_subscription.current.id, "/"))[1]}
Directory ID: ${data.azurerm_client_config.current.tenant_id}
EOT
}